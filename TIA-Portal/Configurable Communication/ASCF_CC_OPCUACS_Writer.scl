FUNCTION_BLOCK "ASCF_CC_OPCUACS_Writer"
{ S7_Optimized_Access := 'TRUE' }
VERSION : 0.1
   VAR_INPUT 
      Connect : Bool;  
      Disconnect : Bool; 
      DoWrite : Bool;  
      Reset : Bool; 
   END_VAR

   VAR_IN_OUT 
      Connections : Array[*] of "ASCF_CC_OPCUACS_ConnectionInterface";
      Outputs : Array[*] of "ASCF_CL_OutputType";
   END_VAR

   VAR 
      Config { S7_SetPoint := 'False'} : "ASCF_CC_OPCUACS_WriterConfig";  
      Status { ExternalAccessible := 'False'; ExternalVisible := 'False'; ExternalWritable := 'False'; S7_SetPoint := 'False'} : "ASCF_CC_OPCUACS_WriterStatus";   
      internals { ExternalAccessible := 'False'; ExternalVisible := 'False'; ExternalWritable := 'False'} : Struct  
         STATE { ExternalAccessible := 'False'; ExternalVisible := 'False'; ExternalWritable := 'False'} : Struct
            Curr { ExternalAccessible := 'False'; ExternalVisible := 'False'; ExternalWritable := 'False'} : Word := 16#01;
            Prev { ExternalAccessible := 'False'; ExternalVisible := 'False'; ExternalWritable := 'False'} : Word := 16#00;
         END_STRUCT;
         status { ExternalAccessible := 'False'; ExternalVisible := 'False'; ExternalWritable := 'False'} : DWord;
         writerID { ExternalAccessible := 'False'; ExternalVisible := 'False'; ExternalWritable := 'False'} : USInt;
         scheduleIndex { ExternalAccessible := 'False'; ExternalVisible := 'False'; ExternalWritable := 'False'} : USInt;
         tryCount { ExternalAccessible := 'False'; ExternalVisible := 'False'; ExternalWritable := 'False'} : USInt;
         lastCycle { ExternalAccessible := 'False'; ExternalVisible := 'False'; ExternalWritable := 'False'} : Time_Of_Day;
         WriterInput { ExternalAccessible := 'False'; ExternalVisible := 'False'; ExternalWritable := 'False'} : Struct
            VReal { ExternalAccessible := 'False'; ExternalVisible := 'False'; ExternalWritable := 'False'} : Struct
               Value { ExternalAccessible := 'False'; ExternalVisible := 'False'; ExternalWritable := 'False'} : Real;
            END_STRUCT;
            VDInt { ExternalAccessible := 'False'; ExternalVisible := 'False'; ExternalWritable := 'False'} : Struct
               Value { ExternalAccessible := 'False'; ExternalVisible := 'False'; ExternalWritable := 'False'} : DInt;
            END_STRUCT;
            VDWord { ExternalAccessible := 'False'; ExternalVisible := 'False'; ExternalWritable := 'False'} : Struct
               Value { ExternalAccessible := 'False'; ExternalVisible := 'False'; ExternalWritable := 'False'} : DWord;
            END_STRUCT;
            VBool { ExternalAccessible := 'False'; ExternalVisible := 'False'; ExternalWritable := 'False'} : Struct
               Value { ExternalAccessible := 'False'; ExternalVisible := 'False'; ExternalWritable := 'False'} : Bool;
            END_STRUCT;
            VString { ExternalAccessible := 'False'; ExternalVisible := 'False'; ExternalWritable := 'False'} : Struct
               Value { ExternalAccessible := 'False'; ExternalVisible := 'False'; ExternalWritable := 'False'} : String[254];
            END_STRUCT;
         END_STRUCT;
         activeConfig { ExternalAccessible := 'False'; ExternalVisible := 'False'; ExternalWritable := 'False'; S7_SetPoint := 'False'} : "ASCF_CC_OPCUACS_WriterConfig";  
         nodeHandles { ExternalAccessible := 'False'; ExternalVisible := 'False'; ExternalWritable := 'False'} : Array[1..#NODE_COUNT] of DWord;
         nodeStatusList { ExternalAccessible := 'False'; ExternalVisible := 'False'; ExternalWritable := 'False'} : Array[1..#NODE_COUNT] of DWord;
         nodeTimestamps { ExternalAccessible := 'False'; ExternalVisible := 'False'; ExternalWritable := 'False'} : Array[1..#NODE_COUNT] of LDT;
      END_STRUCT;
      OPC_UA_NodeGetHandleList_Instance {InstructionName := 'OPC_UA_NodeGetHandleList'; LibVersion := '1.0'; ExternalAccessible := 'False'; ExternalVisible := 'False'; ExternalWritable := 'False'} : OPC_UA_NodeGetHandleList;
      OPC_UA_WriteList_Instance {InstructionName := 'OPC_UA_WriteList'; LibVersion := '1.0'; ExternalAccessible := 'False'; ExternalVisible := 'False'; ExternalWritable := 'False'} : OPC_UA_WriteList;
      OPC_UA_NodeReleaseHandleList_Instance {InstructionName := 'OPC_UA_NodeReleaseHandleList'; LibVersion := '1.0'; ExternalAccessible := 'False'; ExternalVisible := 'False'; ExternalWritable := 'False'} : OPC_UA_NodeReleaseHandleList;
   END_VAR

   VAR_TEMP 
      tempScheduleIndex : USInt;
      tempGetNodeHandles : Bool;
      tempWrite : Bool;
      tempReleaseNodeHandles : Bool;
      tempStatus : DWord;
      tempTimeNow {InstructionName := 'DTL'; LibVersion := '1.0'} : DTL;
      tempTimeDiff : Time;
      tempLoopVar : USInt;
      tempQC : Byte;
      constErrors : "ASCF_CC_OPCUACS_EnumErrors";
      constConnHandleStates : "ASCF_CC_OPCUACS_EnumConnectionHandleStates";
      constConstants : "ASCF_CC_OPCUACS_Constants";
      constUnionTypes : "ASCF_EnumUnionTypes";
   END_VAR

   VAR CONSTANT 
      NODE_COUNT : USInt := 1;
      MIN_VALID_DATA_TYPE_SELECTOR : USInt := 1;
      MAX_VALID_DATA_TYPE_SELECTOR : USInt := 5;
      STATE_UNINITIALIZED : Word := 16#0001;
      STATE_INITIALIZING : Word := 16#0002;
      STATE_IDLE : Word := 16#0004;
      STATE_GETTING_NODE_HANDLES : Word := 16#0008;
      STATE_EXECUTING : Word := 16#0010;
      STATE_RELEASE_NODE_HANDLES : Word := 16#0020;
      STATE_ERROR : Word := 16#0040;
   END_VAR


BEGIN

	
	
	REGION STATE MACHINE
	    
	    CASE #internals.STATE.Curr OF

	        #STATE_UNINITIALIZED:
	            REGION UNINITIALIZED
	                

	                IF #internals.STATE.Curr <> #internals.STATE.Prev THEN
	                    #internals.STATE.Prev := #internals.STATE.Curr;
	                    
	                    #internals.writerID := 0;
	                    #internals.scheduleIndex := 0;
	                    #internals.tryCount := 0;
	                    #internals.lastCycle := TOD#00:00:00;
	                    
	                    #Status.Cycle := T#0ms;
	                    
	                END_IF;

	                IF #Config.ConnectionIndex <> -1 THEN
	                    
	                
	                    IF #Config.ConnectionIndex >= LOWER_BOUND(ARR := #Connections, DIM := 1) AND #Config.ConnectionIndex <= UPPER_BOUND(ARR := #Connections, DIM := 1) AND
	                        #Config.ValueRegistryIndex >= LOWER_BOUND(ARR := #Outputs, DIM := 1) AND #Config.ValueRegistryIndex <= UPPER_BOUND(ARR := #Outputs, DIM := 1) AND
	                        #Config.DataTypeSelector >= #MIN_VALID_DATA_TYPE_SELECTOR AND #Config.DataTypeSelector <= #MAX_VALID_DATA_TYPE_SELECTOR THEN
	                        
	                        IF #Connections[#Config.ConnectionIndex].Handle.State.Curr = #constConnHandleStates.Initializing THEN
	             
	                            #internals.activeConfig.ConnectionIndex := #Config.ConnectionIndex;
	                            #internals.activeConfig.ValueRegistryIndex := #Config.ValueRegistryIndex;
	                            #internals.activeConfig.DataTypeSelector := #Config.DataTypeSelector;
	                            #internals.activeConfig.Timeout := #Config.Timeout;
	                            #internals.activeConfig.MaxTryCount := #Config.MaxTryCount;
	                            #internals.activeConfig.TargetCycle := #Config.TargetCycle;
	                            
	                            #internals.STATE.Curr := #STATE_INITIALIZING;
	                            
	                        END_IF;
	                        
	                        
	                        
	                    ELSE
	                        #internals.status := #constErrors.HandleIndexOutOfBounds;
	                        #internals.STATE.Curr := #STATE_ERROR;
	                        
	                    END_IF;
	                    
	                END_IF;
	                

	                
	            END_REGION UNINITIALIZED
	            

	        #STATE_INITIALIZING:
	            REGION INITIALIZING
	                

	                IF #internals.STATE.Curr <> #internals.STATE.Prev THEN
	                    #internals.STATE.Prev := #internals.STATE.Curr;

	                    #Connections[#internals.activeConfig.ConnectionIndex].Handle.ScheduleData.WritersCount := #Connections[#internals.activeConfig.ConnectionIndex].Handle.ScheduleData.WritersCount + 1;
	                    #internals.writerID := #Connections[#internals.activeConfig.ConnectionIndex].Handle.ScheduleData.WritersCount;
	                    
	                END_IF;
	                

	                #internals.STATE.Curr := #STATE_IDLE;

	                
	            END_REGION INITIALIZING
	            

	        #STATE_IDLE:
	            REGION IDLE
	                

	                IF #internals.STATE.Curr <> #internals.STATE.Prev THEN
	                    #internals.STATE.Prev := #internals.STATE.Curr;

	                END_IF;
	                

	                IF #Connections[#internals.activeConfig.ConnectionIndex].Handle.ScheduleData.ScheduleGetNodeHandles.Writer AND
	                    "ASCF_CC_OPCUACS_QuerySchedule"(ID := #internals.writerID, Schedule := #Connections[#internals.activeConfig.ConnectionIndex].Handle.ScheduleData.ScheduleGetNodeHandles.Schedule, Index => #tempScheduleIndex) THEN
	                    #Connections[#internals.activeConfig.ConnectionIndex].Handle.ScheduleData.ScheduleGetNodeHandles.Schedule[#tempScheduleIndex] := #constConstants.OperationDoneID;
	                END_IF;
	                
	                IF #Connections[#internals.activeConfig.ConnectionIndex].Handle.ScheduleData.ScheduleReleaseNodeHandles.Writer AND
	                    "ASCF_CC_OPCUACS_QuerySchedule"(ID := #internals.writerID, Schedule := #Connections[#internals.activeConfig.ConnectionIndex].Handle.ScheduleData.ScheduleReleaseNodeHandles.Schedule, Index => #tempScheduleIndex) THEN
	                    #Connections[#internals.activeConfig.ConnectionIndex].Handle.ScheduleData.ScheduleReleaseNodeHandles.Schedule[#tempScheduleIndex] := #constConstants.OperationDoneID;
	                END_IF;
	                
	                IF "ASCF_CC_OPCUACS_QuerySchedule"(ID := #internals.writerID, Schedule := #Connections[#internals.activeConfig.ConnectionIndex].Handle.ScheduleData.ScheduleWrite.Schedule, Index => #tempScheduleIndex) THEN
	                    #Connections[#internals.activeConfig.ConnectionIndex].Handle.ScheduleData.ScheduleWrite.Schedule[#tempScheduleIndex] := #constConstants.OperationDoneID;
	                END_IF;
	                

	                IF #Connections[#internals.activeConfig.ConnectionIndex].Handle.State.Curr = #constConnHandleStates.Error THEN
	                    #internals.STATE.Curr := #STATE_UNINITIALIZED;
	                    
	                ELSIF #Connections[#internals.activeConfig.ConnectionIndex].Handle.State.Curr = #constConnHandleStates.Disconnected THEN
	                    #internals.STATE.Curr := #STATE_UNINITIALIZED;
	                    
	                ELSIF #Connect AND #Connections[#internals.activeConfig.ConnectionIndex].Handle.State.Curr = #constConnHandleStates.Executing THEN
	                    #Connect := false;
	                    
	                    #internals.STATE.Curr := #STATE_GETTING_NODE_HANDLES;
	                    
	                END_IF;
	                

	                
	            END_REGION IDLE

	        #STATE_GETTING_NODE_HANDLES:
	            REGION GETTING NODE HANDLES

	                IF #internals.STATE.Curr <> #internals.STATE.Prev THEN
	                    #internals.STATE.Prev := #internals.STATE.Curr;
	                    #internals.activeConfig.Nodes := #Config.Nodes;
	                    
	                END_IF;
	                

	                IF #Connections[#internals.activeConfig.ConnectionIndex].Handle.ScheduleData.ScheduleReleaseNodeHandles.Writer AND
	                    "ASCF_CC_OPCUACS_QuerySchedule"(ID := #internals.writerID, Schedule := #Connections[#internals.activeConfig.ConnectionIndex].Handle.ScheduleData.ScheduleReleaseNodeHandles.Schedule, Index => #tempScheduleIndex) THEN
	                    #Connections[#internals.activeConfig.ConnectionIndex].Handle.ScheduleData.ScheduleReleaseNodeHandles.Schedule[#tempScheduleIndex] := #constConstants.OperationDoneID;
	                END_IF;
	                
	                IF "ASCF_CC_OPCUACS_QuerySchedule"(ID := #internals.writerID, Schedule := #Connections[#internals.activeConfig.ConnectionIndex].Handle.ScheduleData.ScheduleWrite.Schedule, Index => #tempScheduleIndex) THEN
	                    #Connections[#internals.activeConfig.ConnectionIndex].Handle.ScheduleData.ScheduleWrite.Schedule[#tempScheduleIndex] := #constConstants.OperationDoneID;
	                END_IF;
	                
	                IF NOT (#OPC_UA_NodeGetHandleList_Instance.Busy OR #OPC_UA_NodeGetHandleList_Instance.Done OR #OPC_UA_NodeGetHandleList_Instance.Error) THEN
	                    
	                    IF #Connections[#internals.activeConfig.ConnectionIndex].Handle.ScheduleData.ScheduleGetNodeHandles.Writer AND
	                        "ASCF_CC_OPCUACS_QuerySchedule"(ID := #internals.writerID, Schedule := #Connections[#internals.activeConfig.ConnectionIndex].Handle.ScheduleData.ScheduleGetNodeHandles.Schedule, Index => #internals.scheduleIndex) THEN
	                        #tempGetNodeHandles := true;
	                    END_IF;
	                    
	                END_IF;
	                
	                IF #Connections[#internals.activeConfig.ConnectionIndex].Handle.State.Curr = #constConnHandleStates.Error THEN
	                    #internals.STATE.Curr := #STATE_UNINITIALIZED;
	                    
	                ELSIF #Connections[#internals.activeConfig.ConnectionIndex].Handle.State.Curr = #constConnHandleStates.Disconnected THEN
	                    #internals.STATE.Curr := #STATE_UNINITIALIZED;
	                    
	                ELSIF #OPC_UA_NodeGetHandleList_Instance.Error THEN
	                    #internals.tryCount := #internals.tryCount + 1;
	                    
	                    IF #internals.tryCount >= #internals.activeConfig.MaxTryCount THEN
	                        #internals.status := #OPC_UA_NodeGetHandleList_Instance.Status;
	                        #internals.STATE.Curr := #STATE_ERROR;
	                        
	                        #Connections[#internals.activeConfig.ConnectionIndex].Handle.ScheduleData.ScheduleGetNodeHandles.Schedule[#internals.scheduleIndex] := #constConstants.OperationDoneID;
	                        
	                    END_IF;
	                    
	                ELSIF #OPC_UA_NodeGetHandleList_Instance.Done THEN
	                    #internals.tryCount := 0;
	                    #internals.STATE.Curr := #STATE_EXECUTING;
	                    #Connections[#internals.activeConfig.ConnectionIndex].Handle.ScheduleData.ScheduleGetNodeHandles.Schedule[#internals.scheduleIndex] := #constConstants.OperationDoneID;
	                    
	                    FOR #tempLoopVar := 1 TO #NODE_COUNT DO
	                        IF #internals.nodeHandles[#tempLoopVar] = 16#FFFF_FFFF THEN
	                            #internals.status := #constErrors.InvalidNodeHandle;
	                            #internals.STATE.Curr := #STATE_ERROR;
	                            EXIT;
	                        END_IF;
	                    END_FOR;
	                    
	                END_IF;

	                
	            END_REGION GETTING NODE HANDLES
	            

	        #STATE_EXECUTING:
	            REGION EXECUTING
	                
	                IF #internals.STATE.Curr <> #internals.STATE.Prev THEN
	                    #internals.STATE.Prev := #internals.STATE.Curr;
	                END_IF;

	                IF #Connections[#internals.activeConfig.ConnectionIndex].Handle.ScheduleData.ScheduleGetNodeHandles.Writer AND
	                    "ASCF_CC_OPCUACS_QuerySchedule"(ID := #internals.writerID, Schedule := #Connections[#internals.activeConfig.ConnectionIndex].Handle.ScheduleData.ScheduleGetNodeHandles.Schedule, Index => #tempScheduleIndex) THEN
	                    #Connections[#internals.activeConfig.ConnectionIndex].Handle.ScheduleData.ScheduleGetNodeHandles.Schedule[#tempScheduleIndex] := #constConstants.OperationDoneID;
	                END_IF;
	                
	                IF #Connections[#internals.activeConfig.ConnectionIndex].Handle.ScheduleData.ScheduleReleaseNodeHandles.Writer AND
	                    "ASCF_CC_OPCUACS_QuerySchedule"(ID := #internals.writerID, Schedule := #Connections[#internals.activeConfig.ConnectionIndex].Handle.ScheduleData.ScheduleReleaseNodeHandles.Schedule, Index => #tempScheduleIndex) THEN
	                    #Connections[#internals.activeConfig.ConnectionIndex].Handle.ScheduleData.ScheduleReleaseNodeHandles.Schedule[#tempScheduleIndex] := #constConstants.OperationDoneID;
	                END_IF;
	                
	                IF NOT (#OPC_UA_WriteList_Instance.Busy OR #OPC_UA_WriteList_Instance.Done OR #OPC_UA_WriteList_Instance.Error) THEN
	                    
	                    IF "ASCF_CC_OPCUACS_QuerySchedule"(ID := #internals.writerID, Schedule := #Connections[#internals.activeConfig.ConnectionIndex].Handle.ScheduleData.ScheduleWrite.Schedule, Index => #internals.scheduleIndex) THEN
	                        
	                        IF NOT #DoWrite THEN
	                            #Connections[#internals.activeConfig.ConnectionIndex].Handle.ScheduleData.ScheduleWrite.Schedule[#internals.scheduleIndex] := #constConstants.OperationDoneID;
	                            
	                        ELSE
	                            #tempStatus := INT_TO_DWORD(IN := RD_SYS_T(OUT => #tempTimeNow));
	                            #tempTimeDiff := T_DIFF(IN1 := DTL_TO_TOD(#tempTimeNow), IN2 := #internals.lastCycle);
	                            
	                            IF #tempTimeDiff > #internals.activeConfig.TargetCycle THEN
	                                #internals.lastCycle := DTL_TO_TOD(#tempTimeNow);
	                                #Status.Cycle := #tempTimeDiff;
	                                
	                                #tempWrite := true;
	                                
	                            ELSE
	                                #Connections[#internals.activeConfig.ConnectionIndex].Handle.ScheduleData.ScheduleWrite.Schedule[#internals.scheduleIndex] := #constConstants.OperationDoneID;
	                                
	                            END_IF;
	                            
	                        END_IF;
	                        
	                    END_IF;
	                    
	                END_IF;

	                IF #tempWrite THEN
	                    
	          
	                    IF #internals.activeConfig.DataTypeSelector = #constUnionTypes.VReal THEN
	                        #tempStatus := "ASCF_GetVRealOutputList"(Index := #internals.activeConfig.ValueRegistryIndex, QC => #tempQC, Value => #internals.WriterInput.VReal.Value, Outputs := #Outputs);
	                        
	           
	                    ELSIF #internals.activeConfig.DataTypeSelector = #constUnionTypes.VDInt THEN
	                        #tempStatus := "ASCF_GetVDIntOutputList"(Index := #internals.activeConfig.ValueRegistryIndex, QC => #tempQC, Value => #internals.WriterInput.VDInt.Value, Outputs := #Outputs);
	      
	                    ELSIF #internals.activeConfig.DataTypeSelector = #constUnionTypes.VDWord THEN
	                        #tempStatus := "ASCF_GetVDWordOutputList"(Index := #internals.activeConfig.ValueRegistryIndex, QC => #tempQC, Value => #internals.WriterInput.VDWord.Value, Outputs := #Outputs);
	                        
	          
	                    ELSIF #internals.activeConfig.DataTypeSelector = #constUnionTypes.VBool THEN
	                        #tempStatus := "ASCF_GetVBoolOutputList"(Index := #internals.activeConfig.ValueRegistryIndex, QC => #tempQC, Value => #internals.WriterInput.VBool.Value, Outputs := #Outputs);
	                        
	           
	                    ELSIF #internals.activeConfig.DataTypeSelector = #constUnionTypes.VString THEN
	                        #tempStatus := "ASCF_GetVStringOutputList"(Index := #internals.activeConfig.ValueRegistryIndex, QC => #tempQC, Value => #internals.WriterInput.VString.Value, Outputs := #Outputs);
	                        
	                    END_IF;
	                    

	                    IF #tempStatus <> #constErrors.None THEN
	                        #tempWrite := false;
	                        #internals.status := #tempStatus;
	                        #internals.STATE.Curr := #STATE_ERROR;
	                        
	                        #Connections[#internals.activeConfig.ConnectionIndex].Handle.ScheduleData.ScheduleWrite.Schedule[#internals.scheduleIndex] := #constConstants.OperationDoneID;
	                    END_IF;
	                    
	                END_IF;
	                

	                IF #Connections[#internals.activeConfig.ConnectionIndex].Handle.State.Curr = #constConnHandleStates.Error THEN
	                    #internals.STATE.Curr := #STATE_UNINITIALIZED;
	                    
	                ELSIF #Connections[#internals.activeConfig.ConnectionIndex].Handle.State.Curr = #constConnHandleStates.Disconnected THEN
	                    #internals.STATE.Curr := #STATE_UNINITIALIZED;
	                    
	                ELSIF #Disconnect THEN
	                    #Disconnect := false;
	                    #internals.STATE.Curr := #STATE_RELEASE_NODE_HANDLES;
	                    
	                ELSIF #OPC_UA_WriteList_Instance.Error THEN
	                    #internals.tryCount := #internals.tryCount + 1;
	                    
	                    IF #internals.tryCount >= #internals.activeConfig.MaxTryCount THEN
	                        #internals.status := #OPC_UA_WriteList_Instance.Status;
	                        #internals.STATE.Curr := #STATE_ERROR;
	                        
	                        #Connections[#internals.activeConfig.ConnectionIndex].Handle.ScheduleData.ScheduleWrite.Schedule[#internals.scheduleIndex] := #constConstants.OperationDoneID;
	                        
	                    END_IF;
	                    
	                ELSIF #OPC_UA_WriteList_Instance.Done THEN
	                    #internals.tryCount := 0;
	                    
	                    #DoWrite := false;
	                    
	                    #Connections[#internals.activeConfig.ConnectionIndex].Handle.ScheduleData.ScheduleWrite.Schedule[#internals.scheduleIndex] := #constConstants.OperationDoneID;
	                    
	                END_IF;

	                
	            END_REGION EXECUTING
	            
	            

	        #STATE_RELEASE_NODE_HANDLES:
	            REGION RELEASE NODE HANDLES
	                

	                IF #internals.STATE.Curr <> #internals.STATE.Prev THEN
	                    #internals.STATE.Prev := #internals.STATE.Curr;
	             
	                END_IF;
	                

	                IF #Connections[#internals.activeConfig.ConnectionIndex].Handle.ScheduleData.ScheduleGetNodeHandles.Writer AND
	                    "ASCF_CC_OPCUACS_QuerySchedule"(ID := #internals.writerID, Schedule := #Connections[#internals.activeConfig.ConnectionIndex].Handle.ScheduleData.ScheduleGetNodeHandles.Schedule, Index => #tempScheduleIndex) THEN
	                    #Connections[#internals.activeConfig.ConnectionIndex].Handle.ScheduleData.ScheduleGetNodeHandles.Schedule[#tempScheduleIndex] := #constConstants.OperationDoneID;
	                END_IF;
	                
	      
	                IF "ASCF_CC_OPCUACS_QuerySchedule"(ID := #internals.writerID, Schedule := #Connections[#internals.activeConfig.ConnectionIndex].Handle.ScheduleData.ScheduleWrite.Schedule, Index => #tempScheduleIndex) THEN
	                    #Connections[#internals.activeConfig.ConnectionIndex].Handle.ScheduleData.ScheduleWrite.Schedule[#tempScheduleIndex] := #constConstants.OperationDoneID;
	                END_IF;
	                
	         
	                IF NOT (#OPC_UA_NodeReleaseHandleList_Instance.Busy OR #OPC_UA_NodeReleaseHandleList_Instance.Done OR #OPC_UA_NodeReleaseHandleList_Instance.Error) THEN
	                    
	                    IF #Connections[#internals.activeConfig.ConnectionIndex].Handle.ScheduleData.ScheduleReleaseNodeHandles.Writer AND
	                        "ASCF_CC_OPCUACS_QuerySchedule"(ID := #internals.writerID, Schedule := #Connections[#internals.activeConfig.ConnectionIndex].Handle.ScheduleData.ScheduleReleaseNodeHandles.Schedule, Index => #internals.scheduleIndex) THEN
	                        #tempReleaseNodeHandles := true;
	                    END_IF;
	                    
	                END_IF;
	                
	                

	                IF #Connections[#internals.activeConfig.ConnectionIndex].Handle.State.Curr = #constConnHandleStates.Error THEN
	                    #internals.STATE.Curr := #STATE_UNINITIALIZED;
	                    
	                ELSIF #Connections[#internals.activeConfig.ConnectionIndex].Handle.State.Curr = #constConnHandleStates.Disconnected THEN
	                    #internals.STATE.Curr := #STATE_UNINITIALIZED;
	                    
	                ELSIF #OPC_UA_NodeReleaseHandleList_Instance.Error THEN
	                    #internals.tryCount := #internals.tryCount + 1;
	                    
	                    IF #internals.tryCount >= #internals.activeConfig.MaxTryCount THEN
	                        #internals.status := #OPC_UA_NodeReleaseHandleList_Instance.Status;
	                        #internals.STATE.Curr := #STATE_ERROR;
	                        
	                        #Connections[#internals.activeConfig.ConnectionIndex].Handle.ScheduleData.ScheduleReleaseNodeHandles.Schedule[#internals.scheduleIndex] := #constConstants.OperationDoneID;
	                        
	                    END_IF;
	                    
	                ELSIF #OPC_UA_NodeReleaseHandleList_Instance.Done THEN
	                    #internals.tryCount := 0;
	                    #internals.STATE.Curr := #STATE_IDLE;
	                    
	                    #Connections[#internals.activeConfig.ConnectionIndex].Handle.ScheduleData.ScheduleReleaseNodeHandles.Schedule[#internals.scheduleIndex] := #constConstants.OperationDoneID;
	                    
	                END_IF;
	                

	                
	            END_REGION RELEASE NODE HANDLES
	            

	        #STATE_ERROR:
	            REGION ERROR

	                IF #internals.STATE.Curr <> #internals.STATE.Prev THEN
	                    #internals.STATE.Prev := #internals.STATE.Curr;
	                    
	                    #Status.Error := true;
	                    #Status.Status := #internals.status;
	                    
	                END_IF;
	                
	                

	                #Connections[#internals.activeConfig.ConnectionIndex].Handle.WriterError := true;
	                
	                IF #Connections[#internals.activeConfig.ConnectionIndex].Handle.ScheduleData.ScheduleGetNodeHandles.Writer AND
	                    "ASCF_CC_OPCUACS_QuerySchedule"(ID := #internals.writerID, Schedule := #Connections[#internals.activeConfig.ConnectionIndex].Handle.ScheduleData.ScheduleGetNodeHandles.Schedule, Index => #tempScheduleIndex) THEN
	                    #Connections[#internals.activeConfig.ConnectionIndex].Handle.ScheduleData.ScheduleGetNodeHandles.Schedule[#tempScheduleIndex] := #constConstants.OperationDoneID;
	                END_IF;
	                
	                IF #Connections[#internals.activeConfig.ConnectionIndex].Handle.ScheduleData.ScheduleReleaseNodeHandles.Writer AND
	                    "ASCF_CC_OPCUACS_QuerySchedule"(ID := #internals.writerID, Schedule := #Connections[#internals.activeConfig.ConnectionIndex].Handle.ScheduleData.ScheduleReleaseNodeHandles.Schedule, Index => #tempScheduleIndex) THEN
	                    #Connections[#internals.activeConfig.ConnectionIndex].Handle.ScheduleData.ScheduleReleaseNodeHandles.Schedule[#tempScheduleIndex] := #constConstants.OperationDoneID;
	                END_IF;
	                
	                IF "ASCF_CC_OPCUACS_QuerySchedule"(ID := #internals.writerID, Schedule := #Connections[#internals.activeConfig.ConnectionIndex].Handle.ScheduleData.ScheduleWrite.Schedule, Index => #tempScheduleIndex) THEN
	                    #Connections[#internals.activeConfig.ConnectionIndex].Handle.ScheduleData.ScheduleWrite.Schedule[#tempScheduleIndex] := #constConstants.OperationDoneID;
	                END_IF;
	                
	
	                IF #Connections[#internals.activeConfig.ConnectionIndex].Handle.State.Curr = #constConnHandleStates.Error THEN
	                    #internals.STATE.Curr := #STATE_UNINITIALIZED;
	                    
	                ELSIF #Connections[#internals.activeConfig.ConnectionIndex].Handle.State.Curr = #constConnHandleStates.Disconnected THEN
	                    #internals.STATE.Curr := #STATE_UNINITIALIZED;
	                    
	                ELSIF #Reset THEN
	                    #internals.STATE.Curr := #STATE_RELEASE_NODE_HANDLES;
	                    
	                END_IF;
	                

	                IF #internals.STATE.Curr <> #internals.STATE.Prev THEN
	                    #Reset := false;
	                    
	                    #Status.Error := false;
	                    #Status.Status := #constErrors.None;
	                    
	                END_IF;
	                
	            END_REGION ERROR
	            
	    END_CASE;
	    
	    
	END_REGION STATE MACHINE
	
	
	
	REGION OPC UA CALLS
	    
	    IF #internals.STATE.Curr <> #STATE_UNINITIALIZED THEN
	        
	        #OPC_UA_NodeGetHandleList_Instance(REQ := #tempGetNodeHandles,
	                                           ConnectionHdl := #Connections[#internals.activeConfig.ConnectionIndex].Handle.Handle,
	                                           NodeIDCount := #NODE_COUNT,
	                                           NodeIDs := #internals.activeConfig.Nodes,
	                                           Timeout := #internals.activeConfig.Timeout,
	                                           NamespaceIndexCount := #Connections[#internals.activeConfig.ConnectionIndex].Handle.NamespaceIndexCount,
	                                           NamespaceIndexes := #Connections[#internals.activeConfig.ConnectionIndex].Handle.NamespaceIndexList,
	                                           NodeStatusList := #internals.nodeStatusList,
	                                           NodeHdls := #internals.nodeHandles);
	        

	        IF #internals.activeConfig.DataTypeSelector = #constUnionTypes.VReal THEN
	            
	            #OPC_UA_WriteList_Instance(REQ := #tempWrite,
	                                       ConnectionHdl := #Connections[#internals.activeConfig.ConnectionIndex].Handle.Handle,
	                                       NodeHdlCount := #NODE_COUNT,
	                                       NodeHdls := #internals.nodeHandles,
	                                       Timeout := #internals.activeConfig.Timeout,
	                                       NodeStatusList := #internals.nodeStatusList,
	                                       Variable := #internals.WriterInput.VReal);
	   
	        ELSIF #internals.activeConfig.DataTypeSelector = #constUnionTypes.VDInt THEN
	            
	            #OPC_UA_WriteList_Instance(REQ := #tempWrite,
	                                       ConnectionHdl := #Connections[#internals.activeConfig.ConnectionIndex].Handle.Handle,
	                                       NodeHdlCount := #NODE_COUNT,
	                                       NodeHdls := #internals.nodeHandles,
	                                       Timeout := #internals.activeConfig.Timeout,
	                                       NodeStatusList := #internals.nodeStatusList,
	                                       Variable := #internals.WriterInput.VDInt);
	            
	
	        ELSIF #internals.activeConfig.DataTypeSelector = #constUnionTypes.VDWord THEN
	            
	            #OPC_UA_WriteList_Instance(REQ := #tempWrite,
	                                       ConnectionHdl := #Connections[#internals.activeConfig.ConnectionIndex].Handle.Handle,
	                                       NodeHdlCount := #NODE_COUNT,
	                                       NodeHdls := #internals.nodeHandles,
	                                       Timeout := #internals.activeConfig.Timeout,
	                                       NodeStatusList := #internals.nodeStatusList,
	                                       Variable := #internals.WriterInput.VDWord);

	        ELSIF #internals.activeConfig.DataTypeSelector = #constUnionTypes.VBool THEN
	            
	            #OPC_UA_WriteList_Instance(REQ := #tempWrite,
	                                       ConnectionHdl := #Connections[#internals.activeConfig.ConnectionIndex].Handle.Handle,
	                                       NodeHdlCount := #NODE_COUNT,
	                                       NodeHdls := #internals.nodeHandles,
	                                       Timeout := #internals.activeConfig.Timeout,
	                                       NodeStatusList := #internals.nodeStatusList,
	                                       Variable := #internals.WriterInput.VBool);
	            

	        ELSIF #internals.activeConfig.DataTypeSelector = #constUnionTypes.VString THEN
	            
	            #OPC_UA_WriteList_Instance(REQ := #tempWrite,
	                                       ConnectionHdl := #Connections[#internals.activeConfig.ConnectionIndex].Handle.Handle,
	                                       NodeHdlCount := #NODE_COUNT,
	                                       NodeHdls := #internals.nodeHandles,
	                                       Timeout := #internals.activeConfig.Timeout,
	                                       NodeStatusList := #internals.nodeStatusList,
	                                       Variable := #internals.WriterInput.VString);
	            
	        END_IF;
	        
	        
	        #OPC_UA_NodeReleaseHandleList_Instance(REQ := #tempReleaseNodeHandles,
	                                               ConnectionHdl := #Connections[#internals.activeConfig.ConnectionIndex].Handle.Handle,
	                                               NodeHdlCount := #NODE_COUNT,
	                                               NodeHdls := #internals.nodeHandles,
	                                               Timeout := #internals.activeConfig.Timeout,
	                                               NodeStatusList := #internals.nodeStatusList);
	        
	    END_IF;
	    
	END_REGION OPC UA CALLS
	
	
	REGION OUTPUTS
	    
	    #Status.State := #internals.STATE.Curr;
	    
	END_REGION OUTPUTS
END_FUNCTION_BLOCK